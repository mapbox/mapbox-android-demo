apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.github.triplet.play'
apply from: "$project.rootDir/gradle/script-git-version.gradle"
apply from: "$project.rootDir/gradle/gradle-dependencies-graph.gradle"

android {
    compileSdkVersion androidVersions.compileSdkVersion
    buildToolsVersion androidVersions.buildToolsVersion

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = 'mapbox-android-demo-publish@android-gl-native.iam.gserviceaccount.com'
            jsonFile = file("$project.rootDir/android-gl-native-6d21dd280e7b.json")
        }
    }

    defaultConfig {
        applicationId "com.mapbox.mapboxandroiddemo"

        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion

        versionCode gitVersionCode
        versionName gitVersionName

        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        buildConfigField "int", "MIN_SDK_VERSION", "$minSdkVersion.apiLevel"

        playAccountConfig = playAccountConfigs.defaultAccountConfig
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default"

    productFlavors {
        gpservices {
            resValue "string", "firebase_app_id", "1:557332920931:android:bdf11618ae45dd61"
            resValue "string", "firebase_api_key", "AIzaSyCGCYLyLoi31Hv-pkBO0D51zCyxjngJbz8"
        }

        nogpservices {
        }
    }

    signingConfigs {
        release {
            keyAlias System.getenv("CIRCLECI_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("CIRCLECI_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file("$project.rootDir/mapbox_demo.keystore")
            storePassword System.getenv("CIRCLECI_ANDROID_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-Debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable false
            universalApk false
        }
    }

    lintOptions {
        abortOnError false
    }

    play {
        track = 'alpha'
        untrackOld = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // Wearable application module
    gpservicesWearApp project(':MapboxAndroidWearDemo')

    // Support libraries
    implementation dependenciesList.supportGridLayout
    implementation dependenciesList.supportAppcompatV7
    implementation dependenciesList.supportCardView
    implementation dependenciesList.supportRecyclerView
    implementation dependenciesList.supportDesign
    implementation dependenciesList.supportCustomTabs
    implementation dependenciesList.supportV4
    implementation dependenciesList.supportConstraintLayout

    // Mapbox dependencies
    api(dependenciesList.mapboxMapSdk)
    implementation dependenciesList.mapboxTurf

    // Mapbox plugins
    implementation dependenciesList.mapboxPluginLocationLayer
    implementation dependenciesList.mapboxPluginBuilding
    implementation dependenciesList.mapboxPluginPlaces
    implementation dependenciesList.mapboxPluginLocalization
    implementation dependenciesList.mapboxPluginTraffic

    // Firebase
    implementation dependenciesList.firebaseCrash
    implementation dependenciesList.firebasePerf
    gpservicesImplementation dependenciesList.firebaseCrash

    // Other dependencies
    implementation dependenciesList.gson
    implementation dependenciesList.baseFab
    implementation dependenciesList.materialStyleDialog
    implementation dependenciesList.cuttaGifView
    implementation dependenciesList.materialDialogsCommons
    implementation dependenciesList.kotlin
    implementation dependenciesList.multiDex

    // Square crew
    implementation dependenciesList.timber
    implementation dependenciesList.okhttp3
    implementation dependenciesList.picasso
    implementation dependenciesList.retrofit
    implementation dependenciesList.retrofitGsonConverter

    // Common module
    implementation project(':SharedCode')

    // Testing
    implementation dependenciesList.testEspressoCore
    implementation dependenciesList.testEspressoIntents
    implementation dependenciesList.testEspressoContrib
    androidTestImplementation dependenciesList.testRunner
    androidTestImplementation dependenciesList.testRules
    androidTestImplementation dependenciesList.supportAnnotations
    implementation dependenciesList.junit
}

apply from: "${rootDir}/gradle/checkstyle.gradle"
