apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'
apply from: "$project.rootDir/gradle/script-git-version.gradle"
apply from: "$project.rootDir/gradle/gradle-dependencies-graph.gradle"

def isGlobal
android {
    compileSdkVersion androidVersions.compileSdkVersion
    buildToolsVersion androidVersions.buildToolsVersion

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = 'mapbox-android-demo-publish@android-gl-native.iam.gserviceaccount.com'
            jsonFile = file("$project.rootDir/android-gl-native-6d21dd280e7b.json")
        }
    }

    defaultConfig {
        applicationId "com.mapbox.mapboxandroiddemo"

        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion

        versionCode gitVersionCode
        versionName gitVersionName

        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        buildConfigField "int", "MIN_SDK_VERSION", "$minSdkVersion.apiLevel"

        playAccountConfig = playAccountConfigs.defaultAccountConfig
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default"

    productFlavors {
        global {
            isGlobal = true
            resValue "string", "firebase_app_id", "1:557332920931:android:bdf11618ae45dd61"
            resValue "string", "firebase_api_key", "AIzaSyCGCYLyLoi31Hv-pkBO0D51zCyxjngJbz8"
        }

        china {
            isGlobal = false
            repositories {
                maven { url "https://mapbox.bintray.com/mapbox" }
            }
        }
    }

    signingConfigs {
        release {
            keyAlias System.getenv("CIRCLECI_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("CIRCLECI_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file("$project.rootDir/mapbox_demo.keystore")
            storePassword System.getenv("CIRCLECI_ANDROID_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-Debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable false
            universalApk false
        }
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    play {
        track = 'alpha'
        untrackOld = true
    }
}

if (isGlobal) {
    apply plugin: 'com.google.firebase.firebase-perf'
}
dependencies {

    // Wearable application module
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    globalWearApp project(':MapboxAndroidWearDemo')

    // Support libraries
    implementation dependenciesList.supportGridLayout
    implementation dependenciesList.supportAppcompatV7
    implementation dependenciesList.supportCardView
    implementation dependenciesList.supportRecyclerView
    implementation dependenciesList.supportDesign
    implementation dependenciesList.supportCustomTabs
    implementation dependenciesList.supportV4
    implementation dependenciesList.supportConstraintLayout
    implementation dependenciesList.supportAnimation

    // Mapbox dependencies
    chinaImplementation dependenciesList.mapboxChinaPlugin
    implementation dependenciesList.mapboxMapSdk
    implementation dependenciesList.mapboxTurf
    implementation dependenciesList.mapboxServices

    // Mapbox plugins
    implementation dependenciesList.mapboxPluginBuilding
    implementation dependenciesList.mapboxPluginPlaces
    implementation dependenciesList.mapboxPluginLocalization
    implementation dependenciesList.mapboxPluginTraffic
    implementation dependenciesList.mapboxPluginMarkerView
    implementation dependenciesList.mapboxPluginAnnotation

    // Firebase
    globalImplementation dependenciesList.firebaseCrash
    globalImplementation dependenciesList.firebasePerf
    globalImplementation dependenciesList.firebaseCrash

    // Other dependencies
    implementation dependenciesList.gson
    implementation dependenciesList.baseFab
    implementation dependenciesList.materialStyleDialog
    implementation dependenciesList.materialDialogsCommons
    implementation dependenciesList.kotlin
    implementation dependenciesList.multiDex

    // Square crew
    implementation dependenciesList.timber
    implementation dependenciesList.okhttp3
    implementation dependenciesList.picasso
    implementation dependenciesList.retrofit
    implementation dependenciesList.retrofitGsonConverter

    // Common module
    implementation project(':SharedCode')

    // Testing
    implementation dependenciesList.testEspressoCore
    implementation dependenciesList.testEspressoIntents
    implementation dependenciesList.testEspressoContrib
    androidTestImplementation dependenciesList.testRunner
    androidTestImplementation dependenciesList.testRules
    androidTestImplementation dependenciesList.supportAnnotations
    implementation dependenciesList.junit
}

apply from: "${rootDir}/gradle/checkstyle.gradle"
apply plugin: 'kotlin-android-extensions'
